buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.zinuzoid.twowayhashmap'
version = '0.1.0'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "0.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.file('bintray.properties').newDataInputStream())
bintray {
    user = bintrayProperties.getProperty('BinTray.user')
    key = bintrayProperties.getProperty('BinTray.key')

    configurations = ['archives']
    dryRun = false
    pkg {
        repo = 'Android'
        name = 'com.zinuzoid.twowayhashmap'

        websiteUrl = 'https://github.com/zinuzoid/TwoWayHashMap'
        issueTrackerUrl = 'https://github.com/zinuzoid/TwoWayHashMap/issues'
        vcsUrl = 'https://github.com/zinuzoid/TwoWayHashMap.git'
        licenses = ['GPL-3.0']
        labels = ['android', 'hashmap', 'twoway']

        version {
            name = '0.1.0'
            vcsTag = '0.1.0'
        }
    }
}



